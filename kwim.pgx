# Pegex grammar for the Kwim markup language
#
#   Copyright 2014. Ingy d√∂t Net <ingy@ingy.net>
#

%grammar kwim
%version 0.0.1

document: block-top*

block-top:
  | block-blank
  | block-comment
  | line-comment
  | block-head
  | block-pref
  | block-list
  | block-title
  | block-verse
  | block-para

block-blank: line-blank

block-comment: /
  HASH HASH HASH EOL
  (
    (: ANY*? EOL)*?
  )
  HASH HASH HASH EOL
  line-blank?
/

line-comment: /
  HASH SPACE? ( ANY*? ) EOL
  line-blank?
/

block-head: /
  ( EQUAL{1,4} ) SPACE+ (:
    ( ANY+? ) SPACE+ EQUAL+ EOL
  | ( ANY+ EOL (: [^ WS ] ANY* EOL )* [^ WS ] ANY*? ) SPACE+ EQUAL+ EOL
  | ( ANY+ EOL (: [^ WS ] ANY* EOL )*) (= [ char-block-start ] | EOL | EOS)
  ) line-blank?
/

block-pref: /
  (
    (:
      line-blank*
      SPACE SPACE ANY* EOL
    )+
  )
  line-blank?
/

block-list: /(
  line-list-item
  (: line-list-item | line-blank* line-indented )*
  line-blank?
)/

line-list-item: /
  STAR SPACE ANY* EOL
/

block-list-item: (
  | block-blank
  | block-comment
  | line-comment
  | block-head
  | block-pref
  | block-list
  | block-title
  | block-verse
  | block-para
)*

line-indented: /
  SPACE SPACE ANY* EOL
/

block-title: /
  ( text-line )
  EQUAL{3,} EOL
  line-blank?
/

block-verse: /
  DOT EOL
  ( text-line+ )
  line-blank?
/

block-para: /
  ( text-line+ )
  line-blank?
/

text-markup: phrase-markup+

phrase-markup:
  | phrase-text
  | char-escape
  | phrase-bold
  | phrase-emph
  | phrase-code
  | phrase-hyper
  | char-next

char-escape: / BACK ( ANY ) /

phrase-text: /
  (
    (: (! [ char-phrase-start ] |https? COLON ) ALL)+
  )
/

phrase-code: /
  GRAVE
  ( [^ GRAVE]*? )
  GRAVE
/

phrase-bold:
  char-bold
  ( !char-bold phrase-markup )+
  char-bold

phrase-emph:
  char-emph
  ( !char-emph phrase-markup )+
  char-emph

phrase-hyper:
  | phrase-hyper-named
  | phrase-hyper-explicit
  | phrase-hyper-implicit

phrase-hyper-named: /
  DOUBLE ( [^ DOUBLE ]+ ) DOUBLE
  LANGLE ( NS*? ) RANGLE
/

phrase-hyper-explicit: /
  LANGLE ( NS*? ) RANGLE
/

phrase-hyper-implicit: /(https? COLON NS+)/

char-next: / ( ALL ) /

text-line: / (: [^ char-block-start NL ] ANY*? EOL ) /

line-blank: / (: SPACE* EOL ) /

char-block-start: /
  char-pref
  char-list
  char-head
  char-comment
/

char-phrase-start: /
  char-bold
  char-emph
  char-code
  char-hyper
  char-esc
/

char-pref: / SPACE /
char-list: / STAR /
char-head: / EQUAL /
char-comment: / HASH /
char-bold: / STAR /
char-emph: / SLASH /
char-code: / GRAVE /
char-hyper: / DOUBLE LANGLE /
char-esc: / BACK /

# vim: set lisp sw=2:
